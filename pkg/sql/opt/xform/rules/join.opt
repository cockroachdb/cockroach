# =============================================================================
# join.opt contains exploration rules for the Join operator.
# =============================================================================

# CommuteJoin creates a Join with the left and right inputs swapped. This is
# useful for other rules that convert joins to other operators (like merge
# join).
[CommuteJoin, Explore]
(InnerJoin | FullJoin
  $left:*
  $right:*
  $on:*
)
=>
((OpName) $right $left $on)

# CommuteLeftJoin creates a Join with the left and right inputs swapped.
[CommuteLeftJoin, Explore]
(LeftJoin
  $left:*
  $right:*
  $on:*
)
=>
(RightJoin $right $left $on)

# CommuteRightJoin creates a Join with the left and right inputs swapped.
[CommuteRightJoin, Explore]
(RightJoin
  $left:*
  $right:*
  $on:*
)
=>
(LeftJoin $right $left $on)

# GenerateMergeJoins creates MergeJoin operators for the join, using the
# interesting orderings property.
[GenerateMergeJoins, Explore]
(JoinNonApply $left:* $right:* $on:*)
=>
(ConstructMergeJoins (OpName) $left $right $on)

# GenerateLookupJoin creates a LookupJoin alternative for a Join which has a
# Scan as its right input.
[GenerateLookupJoin, Explore]
(InnerJoin | LeftJoin
    $left:*
    (Scan $scanDef:*)
    $on:* & (CanDoLookupJoin $left $scanDef $on)
)
=>
(ConstructLookupJoin (OpName) $left $scanDef $on)

# GenerateLoopJoinWithFilter creates a LookupJoin alternative for a Join which
# has a Select->Scan complex as its right input. The filter can get merged with
# the ON condition (this is correct for both inner and left join).
[GenerateLookupJoinWithFilter, Explore]
(InnerJoin | LeftJoin 
    $left:*
    (Select
        (Scan $scanDef:*)
        $filter:*
    )
    $on:* & (CanDoLookupJoin $left $scanDef $on)
)
=>
(ConstructLookupJoin (OpName) $left $scanDef (ConcatFilters $on $filter))
