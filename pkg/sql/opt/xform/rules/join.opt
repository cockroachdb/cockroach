# =============================================================================
# join.opt contains exploration rules for the Join operator.
# =============================================================================

# We don't allow any of the logical join -> logical join rules (CommuteJoin,
# CommuteLeftJoin, CommuteRightJoin, AssociateJoin) to operate on inputs with
# no columns. This is because zero-column groups can occur multiple times in
# the same normalized tree, and exploration can cause group collisions:
#
# Let A be the 0-column values node with two rows `VALUES (), ()`,
# and B be the 0-column values node with three rows `VALUES (), (), ()`.
#
# Then consider the following query:
#
# (A JOIN B) UNION (B JOIN A)
#
# During build, we add `A JOIN B` and `B JOIN A` to *separate memo groups*.
# Then, during exploration, we apply the `CommuteJoin` rule to transform `A
# JOIN B` to `B JOIN A`. This attempts to get interned into the same group, but
# the interner finds that `B JOIN A` already exists in a different group, and
# panics.
# TODO(justin): find a more long-term solution to this problem.

# CommuteJoin creates a Join with the left and right inputs swapped. This is
# useful for other rules that convert joins to other operators (like merge
# join).
[CommuteJoin, Explore]
(InnerJoin | FullJoin
  $left:* & ^(HasNoCols $left)
  $right:* & ^(HasNoCols $right)
  $on:*
)
=>
((OpName) $right $left $on)

# CommuteLeftJoin creates a Join with the left and right inputs swapped.
[CommuteLeftJoin, Explore]
(LeftJoin
  $left:* & ^(HasNoCols $left)
  $right:* & ^(HasNoCols $left)
  $on:*
)
=>
(RightJoin $right $left $on)

# CommuteRightJoin creates a Join with the left and right inputs swapped.
[CommuteRightJoin, Explore]
(RightJoin
  $left:* & ^(HasNoCols $left)
  $right:* & ^(HasNoCols $left)
  $on:*
)
=>
(LeftJoin $right $left $on)

# GenerateMergeJoins creates MergeJoin operators for the join, using the
# interesting orderings property.
[GenerateMergeJoins, Explore]
(JoinNonApply $left:* $right:* $on:*)
=>
(GenerateMergeJoins (OpName) $left $right $on)

# GenerateLookupJoins creates LookupJoin operators for all indexes (of the Scan
# table) which allow it (including non-covering indexes). See the
# GenerateLookupJoins custom function for more details.
[GenerateLookupJoins, Explore]
(InnerJoin | LeftJoin
    $left:*
    (Scan $scanPrivate:*) & (IsCanonicalScan $scanPrivate)
    $on:*
)
=>
(GenerateLookupJoins (OpName) $left $scanPrivate $on)

# GenerateZigzagJoins creates ZigzagJoin operators for all index pairs (of the
# Scan table) where the prefix column(s) of both indexes is/are fixed to
# constant values in the filters. See comments in GenerateZigzagJoin and
# distsqlrun/zigzagjoiner.go for more details on when a zigzag join can be
# planned.
[GenerateZigzagJoins, Explore]
(Select
    (Scan $scan:*) & (IsCanonicalScan $scan)
    $filters:*
)
=>
(GenerateZigzagJoins $scan $filters)

# GenerateInvertedIndexZigzagJoins creates ZigzagJoin operators for inverted
# indexes that can be constrained with two or more distinct constant values.
# Inverted indexes contain one row for each path-to-leaf in a JSON value, so one
# row in the primary index could generate multiple inverted index keys. This
# property can be exploited by zigzag joining on the same inverted index, fixed
# at any two of the JSON paths we are querying for.
[GenerateInvertedIndexZigzagJoins, Explore]
(Select
    (Scan $scan:*) & (IsCanonicalScan $scan) & (HasInvertedIndexes $scan)
    $filters:*
)
=>
(GenerateInvertedIndexZigzagJoins $scan $filters)

# GenerateLookupJoinWithFilter creates a LookupJoin alternative for a Join which
# has a Select->Scan combination as its right input. The filter can get merged
# with the ON condition (this is correct for both inner and left join).
[GenerateLookupJoinsWithFilter, Explore]
(InnerJoin | LeftJoin
    $left:*
    (Select
        (Scan $scanPrivate:*) & (IsCanonicalScan $scanPrivate)
        $filters:*
    )
    $on:*
)
=>
(GenerateLookupJoins (OpName) $left $scanPrivate (ConcatFilters $on $filters))

# AssociateJoin applies the rule of join associativity. It converts an
# expression like:
#   (A JOIN B ON A.y = B.y) JOIN C ON B.x = C.x
# to the logically equivalent expression:
#   A JOIN (B JOIN C ON B.x = C.x) ON A.y = B.y
[AssociateJoin, Explore]
(InnerJoin
    $left:(InnerJoin
        $innerLeft:* & ^(HasNoCols $innerLeft)
        $innerRight:* & ^(HasNoCols $innerRight)
        $innerOn:*
    )
    $right:* & (ShouldReorderJoins $left $right) & ^(HasNoCols $right)
    $on:*
)
=>
(InnerJoin
    $innerLeft
    (InnerJoin
        $innerRight
        $right
        (ExtractBoundConditions $on (OutputCols2 $innerRight $right))
    )
    (SortFilters
        (ConcatFilters
            (ExtractUnboundConditions $on (OutputCols2 $innerRight $right))
            $innerOn
        )
    )
)
