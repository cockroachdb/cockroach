# =============================================================================
# bool.opt contains normalization rules for boolean And, Or, Not, and Filters
# operators.
#
# In order to make filter matching easier, the Filters rules create a Filters
# operator to wrap the boolean conditions in Select and Join operators as a
# list of AND'd expressions. For example:
#   (InnerJoin
#     (Scan a)
#     (Scan b)
#     (Filters
#       (Eq (Variable a) (Variable b))
#       (Gt (Variable b) (Const 5))
#     )
#   )
#
# Now other rules can rely upon the presence of the Filters when matching:
#   (Select
#     $input:*
#     (Filters [ ... $conjunct:* ... ])
#   )
# =============================================================================


# EliminateEmptyFilters maps an empty Filters list to True.
[EliminateEmptyFilters, Normalize]
(Filters
    $conditions:[]
)
=>
(True)

# EliminateSingletonAndOr discards an And or Or operator that has only one
# condition.
[EliminateSingletonAndOr, Normalize]
(And | Or
    $conditions:[ $item:* ]
)
=>
$item

# SimplifyAnd simplifies the And operator in several possible ways:
#   - Removes True operands
#   - Replaces the And operator with False if any operand is False
#   - Flattens nested And operands by merging their conditions into parent And
# This rule is especially important when we build a simplified filter.
[SimplifyAnd, Normalize]
(And
    $conditions:[ ... (And | True | False) ... ]
)
=>
(SimplifyAnd $conditions)

# SimplifyOr simplifies the Or operator in several possible ways:
#   - Removes False operands
#   - Replaces the Or operator with True if any operand is True
#   - Flattens nested Or operands by merging their conditions into parent Or
# This rule is especially important when we build a simplified filter.
[SimplifyOr, Normalize]
(Or
    $conditions:[ ... (Or | True | False) ... ]
)
=>
(SimplifyOr $conditions)

# SimplifyFilters simplifies the Filters operator in several possible ways:
#   - Removes True operands
#   - Replaces the Filters operator with False if any operand is False or Null
#   - Flattens nested And operands by merging their conditions into parent
#
# Note that the Null handling behavior is different than the SimplifyAnd rule,
# because Filters only appears as a Select or Join filter condition, both of
# which treat a Null filter conjunct exactly as if it were False.
[SimplifyFilters, Normalize]
(Filters
    $conditions:[ ... (And | True | False | Null) ... ]
)
=>
(SimplifyFilters $conditions)

# FoldNullAndOr replaces the operator with Null if all of its operands are
# Null.
[FoldNullAndOr, Normalize]
(And | Or
    $conditions:[ (Null) ... ] & (ListOnlyHasNulls $conditions)
)
=>
(Null (BoolType))

# NegateComparison inverts eligible comparison operators when they are negated
# by the Not operator. For example, Eq maps to Ne, and Gt maps to Le. All
# comparisons can be negated except for the JSON comparisons.
[NegateComparison, Normalize]
(Not $input:(Comparison $left:* $right:*) & ^(Contains|JsonExists|JsonSomeExists|JsonAllExists))
=>
(NegateComparison (OpName $input) $left $right)

# EliminateNot discards a doubled Not operator.
[EliminateNot, Normalize]
(Not (Not $input:*)) => $input

# NegateAnd converts the negation of a conjunction into a disjunction of
# negations.
[NegateAnd, Normalize]
(Not (And $conditions:*))
=>
(Or (NegateConditions $conditions))

# NegateOr converts the negation of a disjunction into a conjunction of
# negations.
[NegateOr, Normalize]
(Not (Or $conditions:*))
=>
(And (NegateConditions $conditions))

# ExtractRedundantClause matches an OR expression in which one of the OR
# conditions appears as a conjunct in all of the other OR conditions. For
# example, it matches expressions such as: A OR (A AND B)
#
# In this case, A is the redundant clause, so the boolean expression can be
# simplified to:
#   A OR (A AND B)  =>  A
[ExtractRedundantClause, Normalize]
(Or $conditions:[ $item:* ... ] & (IsRedundantSubclause $conditions $item))
=>
$item

# ExtractRedundantSubclause matches an OR expression in which a subclause
# appears as a conjunct in all of the OR conditions. For example, it matches
# expressions such as: (A AND B) OR (A AND C)
#
# In this case, A is the redundant subclause, so the boolean expression can be
# rewritten as:
#   (A AND B) OR (A AND C)  =>  A AND (B OR C)
#
# This transformation is useful for finding a conjunct that can be pushed
# down in the query tree. For example, if the redundant subclause A is
# fully bound by one side of a join, it can be pushed through the join, even
# if B AND C cannot.
[ExtractRedundantSubclause, Normalize]
(Or $conditions:[ (And [ ... $item:* ... ]) ... ] & (IsRedundantSubclause $conditions $item))
=>
(ExtractRedundantSubclause $conditions $item)
