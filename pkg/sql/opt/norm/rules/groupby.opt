# =============================================================================
# groupby.opt contains normalization rules for the GroupBy operator.
# =============================================================================

# ConvertGroupByToDistinct converts a GroupBy operator that has no aggregations
# to an equivalent DistinctOn operator.
[ConvertGroupByToDistinct, Normalize]
(GroupBy
    $input:*
    $aggregations:[]
    $groupingPrivate:*
)
=>
(DistinctOn $input $aggregations $groupingPrivate)

# EliminateDistinct discards a DistinctOn operator that is eliminating duplicate
# rows by using grouping columns that are statically known to form a strong key.
# By definition, a strong key does not allow duplicate values, so the GroupBy is
# redundant and can be eliminated.
#
# Since a DistinctOn operator can serve as a projection operator, we need to
# replace it with a Project so that the correct columns are projected. The
# project itself may be eliminated later by other rules.
[EliminateDistinct, Normalize]
(DistinctOn
    $input:*
    $aggs:*
    $groupingPrivate:* & (ColsAreKey (GroupingCols $groupingPrivate) $input)
)
=>
(Project
  $input
  []
  (GroupingOutputCols $groupingPrivate $aggs)
)

# EliminateGroupByProject discards a nested Project operator that is only
# removing columns from its input (and not synthesizing new ones). That's
# something the GroupBy operators can do on their own.
[EliminateGroupByProject, Normalize]
(GroupBy | ScalarGroupBy | DistinctOn
    $input:(Project $innerInput:*) & (ColsAreSubset (OutputCols $input) (OutputCols $innerInput))
    $aggregations:*
    $groupingPrivate:*
)
=>
((OpName)
    $innerInput
    $aggregations
    $groupingPrivate
)

# ReduceGroupingCols eliminates redundant grouping columns from the GroupBy
# operator and replaces them by ConstAgg aggregate functions. A grouping
# column is redundant if it is functionally determined by the other grouping
# columns. If that's true, then its value must be constant within a group.
# Therefore, it has no effect on the grouping and can instead be represented as
# an ConstAgg aggregate, since all rows in the group have the same value for
# that column.
[ReduceGroupingCols, Normalize]
(GroupBy | DistinctOn
    $input:*
    $aggregations:*
    $groupingPrivate:* & (CanReduceGroupingCols $input $groupingPrivate)
)
=>
((OpName)
    $input
    (AppendReducedGroupingCols $input $aggregations $groupingPrivate)
    (ReduceGroupingCols $input $groupingPrivate)
)

# EliminateAggDistinctForKeys eliminates unnecessary AggDistinct modifiers when
# it is known that the aggregation argument is unique within each group.
[EliminateAggDistinctForKeys, Normalize]
(GroupBy | ScalarGroupBy
    $input:*
    $aggregations:*
    $groupingPrivate:* & (CanRemoveAggDistinctForKeys $aggregations $groupingPrivate $input)
)
=>
((OpName)
    $input
    (RemoveAggDistinctForKeys $aggregations $groupingPrivate $input)
    $groupingPrivate
)

# EliminateDistinctOnNoColumns eliminates a DistinctOn with no grouping columns,
# replacing it with a projection and a LIMIT 1. For example:
#   SELECT DISTINCT ON (a) a, b FROM ab WHERE a=1
# is equivalent to:
#   SELECT a, b FROM ab WHERE a=1 LIMIT 1
[EliminateDistinctOnNoColumns, Normalize]
(DistinctOn
    $input:*
    $aggregations:*
    $groupingPrivate:* & (HasNoGroupingCols $groupingPrivate)
)
=>
(ConstructProjectionFromDistinctOn
    (Limit $input (Const 1) (GroupingInputOrdering $groupingPrivate))
    $aggregations
)
