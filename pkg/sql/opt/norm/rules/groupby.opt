# =============================================================================
# groupby.opt contains normalization rules for the GroupBy operator.
# =============================================================================

# EliminateDistinct discards a GroupBy operator that is eliminating duplicate
# rows by using grouping columns that are statically known to form a strong key.
# By definition, a strong key does not allow duplicate values, so the GroupBy is
# redundant and can be eliminated.
[EliminateDistinct, Normalize]
(GroupBy
    $input:*
    $aggregations:* & (HasNoCols $aggregations)
    $def:* & (GroupingColsAreKey $def $input)
)
=>
$input

# EliminateGroupByProject discards a nested Project operator that is only
# removing columns from its input (and not synthesizing new ones). That's
# something the GroupBy operators can do on their own.
[EliminateGroupByProject, Normalize]
(GroupBy | ScalarGroupBy
    $input:(Project $innerInput:*) & (HasSubsetCols $input $innerInput)
    $aggregations:*
    $def:*
)
=>
((OpName)
    $innerInput
    $aggregations
    $def
)

# ReduceGroupingCols eliminates redundant grouping columns from the GroupBy
# operator and replaces them by AnyNotNull aggregate functions. A grouping
# column is redundant if it is functionally determined by the other grouping
# columns. If that's true, then its value must be constant within a group.
# Therefore, it has no effect on the grouping and can instead be represented as
# an AnyNotNull aggregate, since all rows in the group have the same value for
# that column.
[ReduceGroupingCols, Normalize]
(GroupBy
    $input:*
    $aggregations:*
    $def:* & (CanReduceGroupingCols $input $def)
)
=>
(GroupBy
    $input
    (AppendReducedGroupingCols $input $aggregations $def)
    (ReduceGroupingCols $input $def)
)
