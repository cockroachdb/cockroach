# =============================================================================
# groupby.opt contains normalization rules for the GroupBy operator.
# =============================================================================

# EliminateDistinct discards a GroupBy operator that is eliminating duplicate
# rows by using grouping columns that are statically known to form a strong key.
# By definition, a strong key does not allow duplicate values, so the GroupBy is
# redundant and can be eliminated.
[EliminateDistinct, Normalize]
(GroupBy
    $input:*
    $aggregations:* & (HasNoCols $aggregations)
    $def:* & (ColsAreKey $def $input)
)
=>
$input

# EliminateGroupByProject discards a nested Project operator that is only
# removing columns from its input (and not synthesizing new ones). That's
# something the GroupBy operator can do on its own.
[EliminateGroupByProject, Normalize]
(GroupBy
    $input:(Project $innerInput:*) & (HasSubsetCols $input $innerInput)
    $aggregations:*
    $def:*
)
=>
(GroupBy
    $innerInput
    $aggregations
    $def
)
