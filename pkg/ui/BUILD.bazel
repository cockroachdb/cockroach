load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary")
load("@io_bazel_rules_go//go:def.bzl", "go_library")

go_library(
    name = "ui",
    srcs = ["ui.go"],
    # keep
    embedsrcs = select({
        "//pkg/ui:with_ui": [
            "dist_vendor/list.min.js",
            "dist_vendor/.gitkeep",
        ],
        "//conditions:default": [
            "dist_vendor/.gitkeep",
        ],
    }),
    importpath = "github.com/cockroachdb/cockroach/pkg/ui",
    visibility = ["//visibility:public"],
    deps = [
        "//pkg/base",
        "//pkg/build",
        "//pkg/util/log",
        "@com_github_cockroachdb_errors//:errors",
    ],
)

bool_flag(
    name = "with_ui_flag",
    build_setting_default = False,
    visibility = ["//visibility:public"],
)

config_setting(
    name = "with_ui",
    flag_values = {
        ":with_ui_flag": "true",
    },
    visibility = ["//visibility:public"],
)

genrule(
    name = "gen-empty-index.html",
    outs = ["index.html"],
    cmd = """
    cat >$@ <<EOF
<!DOCTYPE html>
<title>CockroachDB</title>
Binary built without web UI.
<hr>
<em>%s</em>
EOF
""",
    visibility = ["//pkg/ui:__subpackages__"],
)

genrule(
    name = "listjs",
    srcs = ["@npm//:node_modules/list.js/dist/list.min.js"],
    outs = ["dist_vendor/list.min.js"],
    cmd = "cp ./$(location @npm//:node_modules/list.js/dist/list.min.js) $@",
    tools = ["@npm//list.js"],
)

test_suite(
    name = "lint",
    tests = [
        "//pkg/ui/workspaces/cluster-ui:lint",
        "//pkg/ui/workspaces/db-console:lint",
    ],
)
