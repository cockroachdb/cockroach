load("@io_bazel_rules_go//go:def.bzl", "go_library")
load("@build_bazel_rules_nodejs//:index.bzl", "nodejs_binary")

go_library(
    name = "ui",
    srcs = [
        "tracing_ui.go",
        "ui.go",
    ],
    # keep
    embedsrcs = select({
        "//pkg/ui:cockroach_with_ui": [
            "dist_vendor/list.min.js",
            "templates/tracing/html_template.html",
            "dist_vendor/.gitkeep",
        ],
        "//conditions:default": [
            "templates/tracing/html_template.html",
            "dist_vendor/.gitkeep",
        ],
    }),
    importpath = "github.com/cockroachdb/cockroach/pkg/ui",
    visibility = ["//visibility:public"],
    deps = [
        "//pkg/base",
        "//pkg/build",
        "//pkg/util/log",
        "@com_github_cockroachdb_errors//:errors",
    ],
)

config_setting(
    name = "cockroach_with_ui",
    define_values = {
        "cockroach_with_ui": "y",
    },
    visibility = ["//visibility:public"],
)

genrule(
    name = "gen-empty-index.html",
    outs = ["index.html"],
    cmd = """
    cat >$@ <<EOF
<!DOCTYPE html>
<title>CockroachDB</title>
Binary built without web UI.
<hr>
<em>%s</em>
EOF
""",
    visibility = ["//pkg/ui:__subpackages__"],
)

genrule(
    name = "listjs",
    srcs = ["@npm//:node_modules/list.js/dist/list.min.js"],
    outs = ["dist_vendor/list.min.js"],
    cmd = "cp ./$(location @npm//:node_modules/list.js/dist/list.min.js) $@",
    tools = ["@npm//list.js"],
)
