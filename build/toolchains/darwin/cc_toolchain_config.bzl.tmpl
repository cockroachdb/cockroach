load("@bazel_tools//tools/build_defs/cc:action_names.bzl", "ACTION_NAMES")
load("@bazel_tools//tools/cpp:cc_toolchain_config_lib.bzl",
     "env_entry",
     "env_set",
     "feature",
     "flag_group",
     "flag_set",
     "tool_path")

all_compile_actions = [
    ACTION_NAMES.c_compile,
    ACTION_NAMES.cpp_compile,
    ACTION_NAMES.linkstamp_compile,
    ACTION_NAMES.assemble,
    ACTION_NAMES.preprocess_assemble,
    ACTION_NAMES.cpp_header_parsing,
    ACTION_NAMES.cpp_module_compile,
    ACTION_NAMES.cpp_module_codegen,
    ACTION_NAMES.clif_match,
    ACTION_NAMES.lto_backend,
]

all_link_actions = [
    ACTION_NAMES.cpp_link_executable,
    ACTION_NAMES.cpp_link_dynamic_library,
    ACTION_NAMES.cpp_link_nodeps_dynamic_library,
]

all_archive_actions = [
    ACTION_NAMES.cpp_link_static_library,
]

def _impl(ctx):
    tool_paths = [
        tool_path(
            name = "gcc",
            path = "bin/%{target}-apple-darwin21.2-cc",
        ),
        tool_path(
            name = "ld",
            path = "bin/%{target}-apple-darwin21.2-ld",
        ),
        tool_path(
            name = "cpp",
            path = "bin/%{target}-apple-darwin21.2-c++",
        ),
        tool_path(
            name = "gcov",
            path = "bin/%{target}-apple-darwin21.2-gcov",
        ),
        tool_path(
            name = "nm",
            path = "bin/%{target}-apple-darwin21.2-nm",
        ),
        tool_path(
            name = "objdump",
            path = "bin/%{target}-apple-darwin21.2-objdump",
        ),
        tool_path(
            name = "strip",
            path = "bin/%{target}-apple-darwin21.2-strip",
        ),
        tool_path(
            name = "ar",
            path = "bin/%{target}-apple-darwin21.2-ar",
        ),
    ]

    opt_feature = feature(
        name = "opt",
        flag_sets = [
            flag_set(
                actions = all_compile_actions,
                flag_groups = ([
                    flag_group(
                        flags = [
                            "-O3",
                        ]
                    ),
                ]),
            ),
        ],
    )
    fastbuild_feature = feature(name = "fastbuild")
    dbg_feature = feature(
        name = "dbg",
        flag_sets = [
            flag_set(
                actions = all_compile_actions,
                flag_groups = ([
                    flag_group(
                        flags = [
                            "-g3",
                        ]
                    ),
                ]),
            ),
        ],
    )

    supports_pic_feature = feature(name = "supports_pic", enabled = True)
    supports_dynamic_linker_feature = feature(name = "supports_dynamic_linker", enabled = False)

    default_compile_flags = feature(
        name = "default_compile_flags",
        enabled = True,
        flag_sets = [
            flag_set(
                actions = all_compile_actions,
                flag_groups = ([
                    flag_group(
                        flags = [
                            "-g1",
                            "-Wall",
                        ],
                    ),
                ]),
            ),
        ],
    )

    linker_flags = [
        "-lstdc++",
        "-Lexternal/%{repo_name}/lib",
    ]

    default_linker_flags = feature(
        name = "default_linker_flags",
        enabled = True,
        flag_sets = [
            flag_set(
                actions = all_link_actions,
                flag_groups = ([flag_group(flags = linker_flags)]),
            ),
        ],
	env_sets = [
            env_set(
                actions = all_link_actions,
                env_entries = [
                    env_entry(key = "LD_LIBRARY_PATH", value = "external/%{repo_name}/lib"),
                ],
            ),
	]
    )

    default_archiver_flags = feature(
        name = "archiver_flags",
        enabled = True,
        flag_sets = [
            flag_set(
                actions = all_archive_actions,
                flag_groups = [
                    # NOTE(ricky): Bazel defaults to using rcsD here -- the
                    # macOS ar doesn't understand the D flag, though.
                    flag_group(flags = ["rcs"]),
                    flag_group(
                        flags = ["%{output_execpath}"],
                        expand_if_available = "output_execpath",
                    ),
                ],
            ),
            flag_set(
                actions = all_archive_actions,
                flag_groups = [
                    flag_group(
                        iterate_over = "libraries_to_link",
                        flag_groups = [
                            flag_group(
                                flags = ["%{libraries_to_link.name}"],
                            ),
                        ],
                        expand_if_available = "libraries_to_link",
                    ),
                ],
            ),
        ],
    )

    features = [
        opt_feature,
        fastbuild_feature,
        dbg_feature,
        supports_pic_feature,
        supports_dynamic_linker_feature,
        default_archiver_flags,
        default_compile_flags,
        default_linker_flags,
    ]

    return cc_common.create_cc_toolchain_config_info(
        ctx = ctx,
        features = features,
        toolchain_identifier = "%{target}-apple-darwin21.2-cross-toolchain",
        host_system_name = "{host}-apple-darwin21.2",
        target_system_name = "%{target}-apple-darwin21.2",
        target_cpu = "%{target}-apple-darwin21.2",
        target_libc = "glibc-2.14",
        compiler = "clang",
        abi_version = "clang-10.0.0",
        abi_libc_version = "%{target}-apple-darwin21.2",
        tool_paths = tool_paths,
        cxx_builtin_include_directories = [
            "%sysroot%/usr/include",
            "/usr/lib/llvm-10/lib/clang/10.0.0/include",
        ],
        builtin_sysroot = "external/%{repo_name}/SDK/MacOSX12.1.sdk",
    )

cc_toolchain_config = rule(
    implementation = _impl,
    attrs = {},
    provides = [CcToolchainConfigInfo],
)
