# NOTE: This code is adapted from upstream at
# https://github.com/aspect-build/bazel-lib/blob/c89ec6a554321cf4ff0430fd388edefb7f606ccc/lib/private/copy_directory_toolchain.bzl#LL155C1-L188C2
# We can't use the upstream version of this code as written so we need to duplicate some stuff.

# Versions of copy_directory and copy_to_directory from bazel-lib (github.com/aspect-build/bazel-lib)
# These are built by `build/scripts/build-bazel-lib-helpers.sh`.
# Update these when updating `aspect_bazel_lib` (see comment in `WORKSPACE`)
_URL_PREFIX = "https://storage.googleapis.com/public-bazel-artifacts/js/aspect-bazel-lib-utils-20260212-171628"
_COPY_DIRECTORY_URL_PREFIX = _URL_PREFIX + "/copy_directory-"

COPY_DIRECTORY_PLATFORMS = {
    "darwin_arm64": struct(
        compatible_with = [
            "@platforms//os:macos",
            "@platforms//cpu:aarch64",
        ],
    ),
    "linux_amd64": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:x86_64",
        ],
    ),
    "linux_arm64": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:aarch64",
        ],
    ),
    "linux_ppc64le": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:ppc64le",
        ],
    ),
    "linux_s390x": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:s390x",
        ],
    ),
    "windows_amd64": struct(
        compatible_with = [
            "@platforms//os:windows",
            "@platforms//cpu:x86_64",
        ],
    ),
    "windows_arm64": struct(
        compatible_with = [
            "@platforms//os:windows",
            "@platforms//cpu:arm64",
        ],
    ),
}

COPY_DIRECTORY_VERSIONS = {
    "darwin_arm64": "8ef738d91d895a200bce3c4494689b088f3246858029185ea65f188f31ba59f1",
    "linux_amd64": "3fc43c39ac37cbb3fb3aafc64a9df161fd7d1b53a44eb6ad001076bf3c6e556e",
    "linux_arm64": "d1336f4cdb0558e8f8787daef9ef47707ce16bd8e2af519752141d0c32a376cc",
    "linux_ppc64le": "488b1622513c3cf02c39a182b684a5f410d7097a6c82dcc3218829f6a47cbcb2",
    "linux_s390x": "24f3d21fd95d8a2b7d871363c9f53576533d334fb8d2aa39d209ccf976b5267d",
    "windows_amd64": "abc73f5993d18c3f6e40d28fd53f8377ad073aea47eb383341bb30837e88cf1c",
}

COPY_TO_DIRECTORY_PLATFORMS = {
    "darwin_arm64": struct(
        compatible_with = [
            "@platforms//os:macos",
            "@platforms//cpu:aarch64",
        ],
    ),
    "linux_amd64": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:x86_64",
        ],
    ),
    "linux_arm64": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:aarch64",
        ],
    ),
    "linux_ppc64le": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:ppc64le",
        ],
    ),
    "linux_s390x": struct(
        compatible_with = [
            "@platforms//os:linux",
            "@platforms//cpu:s390x",
        ],
    ),
    "windows_amd64": struct(
        compatible_with = [
            "@platforms//os:windows",
            "@platforms//cpu:x86_64",
        ],
    ),
    "windows_arm64": struct(
        compatible_with = [
            "@platforms//os:windows",
            "@platforms//cpu:arm64",
        ],
    ),
}

_COPY_TO_DIRECTORY_URL_PREFIX = _URL_PREFIX + "/copy_to_directory-"

COPY_TO_DIRECTORY_VERSIONS = {
    "darwin_arm64": "ce83f31f1e2d876ea9ac069f3029fcc13c17a2e03a0464a18a1052a57f2b41ef",
    "linux_amd64": "a5bcc296b0ea39cae2c4683d6620294667ffc41a41cb43e4a0043f26e92cc2c2",
    "linux_arm64": "e125cd76b160440cc77b19d6ec0cbdb512753ed590bc236f919cbd47164832dd",
    "linux_ppc64le": "5bffdea7d1899ccca450d85f1cde98eb3b83bd230cbf2a8f1de3e4f094ea98e1",
    "linux_s390x": "e6c6598e2ca7d9045c9e80bfaf93965884092d5dadcf7be9c3dd85dd16718c32",
    "windows_amd64": "7faccba7c5e734aa048f4f808ea42510c0053d90dfc9d6459fc6656d48e71557",
}

def _copy_directory_platform_repo_impl(rctx):
    plat = rctx.attr.platform
    is_windows = "windows" in rctx.attr.platform
    url = _COPY_DIRECTORY_URL_PREFIX + plat + (".exe" if is_windows else "")
    rctx.download(
        url = url,
        output = "copy_directory.exe" if is_windows else "copy_directory",
        executable = True,
        sha256 = COPY_DIRECTORY_VERSIONS[plat]
    )
    build_content = """# @generated by @com_github_cockroachdb_cockroach//build:nodejs.bzl
load("@aspect_bazel_lib//lib/private:copy_directory_toolchain.bzl", "copy_directory_toolchain")
exports_files(["{0}"])
copy_directory_toolchain(name = "copy_directory_toolchain", bin = "{0}", visibility = ["//visibility:public"])
""".format("copy_directory.exe" if is_windows else "copy_directory")
    # Base BUILD file for this repository
    rctx.file("BUILD.bazel", build_content)

copy_directory_platform_repo = repository_rule(
    implementation = _copy_directory_platform_repo_impl,
    doc = "Fetch external tools needed for copy_directory toolchain",
    attrs = {
        "platform": attr.string(mandatory = True, values = COPY_DIRECTORY_VERSIONS.keys()),
    },
)

def _copy_to_directory_platform_repo_impl(rctx):
    plat = rctx.attr.platform
    is_windows = "windows" in rctx.attr.platform
    url = _COPY_TO_DIRECTORY_URL_PREFIX + plat + (".exe" if is_windows else "")
    rctx.download(
        url = url,
        output = "copy_to_directory.exe" if is_windows else "copy_to_directory",
        executable = True,
        sha256 = COPY_TO_DIRECTORY_VERSIONS[plat]
    )
    build_content = """# @generated by @com_github_cockroachdb_cockroach//build:nodejs.bzl
load("@aspect_bazel_lib//lib/private:copy_to_directory_toolchain.bzl", "copy_to_directory_toolchain")
exports_files(["{0}"])
copy_to_directory_toolchain(name = "copy_to_directory_toolchain", bin = "{0}", visibility = ["//visibility:public"])
""".format("copy_to_directory.exe" if is_windows else "copy_to_directory")

    # Base BUILD file for this repository
    rctx.file("BUILD.bazel", build_content)

copy_to_directory_platform_repo = repository_rule(
    implementation = _copy_to_directory_platform_repo_impl,
    doc = "Fetch external tools needed for copy_to_directory toolchain",
    attrs = {
        "platform": attr.string(mandatory = True, values = COPY_TO_DIRECTORY_VERSIONS.keys()),
    },
)


def _copy_directory_toolchains_repo_impl(rctx):
    # Expose a concrete toolchain which is the result of Bazel resolving the toolchain
    # for the execution or target platform.
    # Workaround for https://github.com/bazelbuild/bazel/issues/14009
    starlark_content = """# @generated by @aspect_bazel_lib//lib/private:copy_directory_toolchain.bzl

# Forward all the providers
def _resolved_toolchain_impl(ctx):
    toolchain_info = ctx.toolchains["@aspect_bazel_lib//lib:copy_directory_toolchain_type"]
    return [
        toolchain_info,
        toolchain_info.default,
        toolchain_info.copy_directory_info,
        toolchain_info.template_variables,
    ]

# Copied from java_toolchain_alias
# https://cs.opensource.google/bazel/bazel/+/master:tools/jdk/java_toolchain_alias.bzl
resolved_toolchain = rule(
    implementation = _resolved_toolchain_impl,
    toolchains = ["@aspect_bazel_lib//lib:copy_directory_toolchain_type"],
)
"""
    rctx.file("defs.bzl", starlark_content)

    build_content = """# @generated by @aspect_bazel_lib//lib/private:copy_directory_toolchain.bzl
#
# These can be registered in the workspace file or passed to --extra_toolchains flag.
# By default all these toolchains are registered by the copy_directory_register_toolchains macro
# so you don't normally need to interact with these targets.

load(":defs.bzl", "resolved_toolchain")

resolved_toolchain(name = "resolved_toolchain", visibility = ["//visibility:public"])

"""

    for [platform, meta] in COPY_DIRECTORY_PLATFORMS.items():
        build_content += """
toolchain(
    name = "{platform}_toolchain",
    exec_compatible_with = {compatible_with},
    toolchain = "@{user_repository_name}_{platform}//:copy_directory_toolchain",
    toolchain_type = "@aspect_bazel_lib//lib:copy_directory_toolchain_type",
)
""".format(
            platform = platform,
            user_repository_name = rctx.attr.user_repository_name,
            compatible_with = meta.compatible_with,
        )

    # Base BUILD file for this repository
    rctx.file("BUILD.bazel", build_content)

copy_directory_toolchains_repo = repository_rule(
    _copy_directory_toolchains_repo_impl,
    doc = """Creates a repository with toolchain definitions for all known platforms
     which can be registered or selected.""",
    attrs = {
        "user_repository_name": attr.string(doc = "Base name for toolchains repository"),
    },
)

def _copy_to_directory_toolchains_repo_impl(rctx):
    # Expose a concrete toolchain which is the result of Bazel resolving the toolchain
    # for the execution or target platform.
    # Workaround for https://github.com/bazelbuild/bazel/issues/14009
    starlark_content = """# @generated by @aspect_bazel_lib//lib/private:copy_to_directory_toolchain.bzl

# Forward all the providers
def _resolved_toolchain_impl(ctx):
    toolchain_info = ctx.toolchains["@aspect_bazel_lib//lib:copy_to_directory_toolchain_type"]
    return [
        toolchain_info,
        toolchain_info.default,
        toolchain_info.copy_to_directory_info,
        toolchain_info.template_variables,
    ]

# Copied from java_toolchain_alias
# https://cs.opensource.google/bazel/bazel/+/master:tools/jdk/java_toolchain_alias.bzl
resolved_toolchain = rule(
    implementation = _resolved_toolchain_impl,
    toolchains = ["@aspect_bazel_lib//lib:copy_to_directory_toolchain_type"],
)
"""
    rctx.file("defs.bzl", starlark_content)

    build_content = """# @generated by @aspect_bazel_lib//lib/private:copy_to_directory_toolchain.bzl
#
# These can be registered in the workspace file or passed to --extra_toolchains flag.
# By default all these toolchains are registered by the copy_to_directory_register_toolchains macro
# so you don't normally need to interact with these targets.

load(":defs.bzl", "resolved_toolchain")

resolved_toolchain(name = "resolved_toolchain", visibility = ["//visibility:public"])

"""

    for [platform, meta] in COPY_TO_DIRECTORY_PLATFORMS.items():
        build_content += """
toolchain(
    name = "{platform}_toolchain",
    exec_compatible_with = {compatible_with},
    toolchain = "@{user_repository_name}_{platform}//:copy_to_directory_toolchain",
    toolchain_type = "@aspect_bazel_lib//lib:copy_to_directory_toolchain_type",
)
""".format(
            platform = platform,
            user_repository_name = rctx.attr.user_repository_name,
            compatible_with = meta.compatible_with,
        )

    # Base BUILD file for this repository
    rctx.file("BUILD.bazel", build_content)

copy_to_directory_toolchains_repo = repository_rule(
    _copy_to_directory_toolchains_repo_impl,
    doc = """Creates a repository with toolchain definitions for all known platforms
     which can be registered or selected.""",
    attrs = {
        "user_repository_name": attr.string(doc = "Base name for toolchains repository"),
    },
)
