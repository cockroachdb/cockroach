load("@aspect_bazel_lib//lib/private:copy_directory_toolchain.bzl", "copy_directory_toolchains_repo")
load("@aspect_bazel_lib//lib/private:copy_to_directory_toolchain.bzl", "copy_to_directory_toolchains_repo")
load("@rules_nodejs//nodejs:repositories.bzl", "node_repositories")
load("@rules_nodejs//nodejs/private:nodejs_repo_host_os_alias.bzl", "nodejs_repo_host_os_alias")
load("@rules_nodejs//nodejs/private:toolchains_repo.bzl", "toolchains_repo")

_NODE_VERSION = "16.14.2"
_VERSIONS = {
    "darwin_amd64": ("node-v16.14.2-darwin-x64.tar.gz", "node-v16.14.2-darwin-x64", "d3076ca7fcc7269c8ff9b03fe7d1c277d913a7e84a46a14eff4af7791ff9d055"),
    "darwin_arm64": ("node-v16.14.2-darwin-arm64.tar.gz", "node-v16.14.2-darwin-arm64", "a66d9217d2003bd416d3dd06dfd2c7a044c4c9ff2e43a27865790bd0d59c682d"),
    "linux_amd64": ("node-v16.14.2-linux-x64.tar.xz", "node-v16.14.2-linux-x64", "e40c6f81bfd078976d85296b5e657be19e06862497741ad82902d0704b34bb1b"),
    "linux_arm64": ("node-v16.14.2-linux-arm64.tar.xz", "node-v16.14.2-linux-arm64", "f7c5a573c06a520d6c2318f6ae204141b8420386553a692fc359f8ae3d88df96"),
    "windows_amd64": ("node-v16.14.2-win-x64.zip", "node-v16.14.2-win-x64", "4731da4fbb2015d414e871fa9118cabb643bdb6dbdc8a69a3ed563266ac93229"),
}

# Versions of copy_directory and copy_to_directory from bazel-lib (github.com/aspect-build/bazel-lib)
# NOTE(ricky): I had to build my own version of these binaries and mirror them
# myself using a version of the code you can find at aspect-build/bazel-lib#447.
# We can use an updated version when that PR is merged. Note we still need this
# bespoke code to set up the repos because our build process must go through our
# infra/storage.
_COPY_DIRECTORY_URL_PREFIX = "https://storage.googleapis.com/public-bazel-artifacts/js/aspect-bazel-lib-utils-2023-06-05/copy_directory-"

_COPY_DIRECTORY_VERSIONS = {
    "darwin_amd64": "b4d39cd9498b8367ba75ad6c13c7687562dabafbf8c782883815314061f9f043",
    "darwin_arm64": "1fd4268a242181d7cdbee7f8035b34548b895fd9e438fab05d48e4627e072e53",
    "linux_amd64": "ce4aaaf41b3b8f9589290d0f3d657400514b7361b9c27f85ac8f966ee4d663b8",
    "linux_arm64": "51099a643689c2e563ab7cd9e14345dd9670ee4814ac4046501675d402decdf4",
    "windows_amd64": "6df30928734abb48515ea16d1273a829651adb77b9ecbbe49e02d17cfffab519",
}

_COPY_TO_DIRECTORY_URL_PREFIX = "https://storage.googleapis.com/public-bazel-artifacts/js/aspect-bazel-lib-utils-2023-06-05/copy_to_directory-"

_COPY_TO_DIRECTORY_VERSIONS = {
    "darwin_amd64": "dadf2fc200a14968664c4b740a76fcee700cb975eb5bfcd3215d253b97a28b23",
    "darwin_arm64": "97ae06279adf44786c1151aa3e4715474603a4792fa64ec6bccb1b52fa00abc1",
    "linux_amd64": "cfac1d923b7039555265ecf1558200d391ffbed62804a4b8c4510b12a18d6e70",
    "linux_arm64": "5c4c69f6f20ba0d6646435ad9922d6193871f3b4262cbc65295e4b89ece667a4",
    "windows_amd64": "2be5d8b2771ffa3922438cda8899f782046633d6d230f744bf63031888a8bf48",
}

# NOTE: This code is adapted from upstream at
# https://github.com/aspect-build/bazel-lib/blob/c89ec6a554321cf4ff0430fd388edefb7f606ccc/lib/private/copy_directory_toolchain.bzl#LL155C1-L188C2
# We can't use the upstream version of this code as written so we need to duplicate some stuff.
def _copy_directory_platform_repo_impl(rctx):
    plat = rctx.attr.platform
    is_windows = "windows" in rctx.attr.platform
    url = "https://storage.googleapis.com/public-bazel-artifacts/js/aspect-bazel-lib-utils-2023-06-05/copy_directory-" + plat + (".exe" if is_windows else "")
    rctx.download(
        url = url,
        output = "copy_directory.exe" if is_windows else "copy_directory",
        executable = True,
        sha256 = _COPY_DIRECTORY_VERSIONS[plat]
    )
    build_content = """# @generated by @com_github_cockroachdb_cockroach//build:nodejs.bzl
load("@aspect_bazel_lib//lib/private:copy_directory_toolchain.bzl", "copy_directory_toolchain")
exports_files(["{0}"])
copy_directory_toolchain(name = "copy_directory_toolchain", bin = "{0}", visibility = ["//visibility:public"])
""".format("copy_directory.exe" if is_windows else "copy_directory")
    # Base BUILD file for this repository
    rctx.file("BUILD.bazel", build_content)

copy_directory_platform_repo = repository_rule(
    implementation = _copy_directory_platform_repo_impl,
    doc = "Fetch external tools needed for copy_directory toolchain",
    attrs = {
        "platform": attr.string(mandatory = True, values = _COPY_DIRECTORY_VERSIONS.keys()),
    },
)

def _copy_to_directory_platform_repo_impl(rctx):
    plat = rctx.attr.platform
    is_windows = "windows" in rctx.attr.platform
    url = "https://storage.googleapis.com/public-bazel-artifacts/js/aspect-bazel-lib-utils-2023-06-05/copy_to_directory-" + plat + (".exe" if is_windows else "")
    rctx.download(
        url = url,
        output = "copy_to_directory.exe" if is_windows else "copy_to_directory",
        executable = True,
        sha256 = _COPY_TO_DIRECTORY_VERSIONS[plat]
    )
    build_content = """# @generated by @com_github_cockroachdb_cockroach//build:nodejs.bzl
load("@aspect_bazel_lib//lib/private:copy_to_directory_toolchain.bzl", "copy_to_directory_toolchain")
exports_files(["{0}"])
copy_to_directory_toolchain(name = "copy_to_directory_toolchain", bin = "{0}", visibility = ["//visibility:public"])
""".format("copy_to_directory.exe" if is_windows else "copy_to_directory")

    # Base BUILD file for this repository
    rctx.file("BUILD.bazel", build_content)

copy_to_directory_platform_repo = repository_rule(
    implementation = _copy_to_directory_platform_repo_impl,
    doc = "Fetch external tools needed for copy_to_directory toolchain",
    attrs = {
        "platform": attr.string(mandatory = True, values = _COPY_TO_DIRECTORY_VERSIONS.keys()),
    },
)

# Helper function used in WORKSPACE.
# Note this function basically re-implements the functionality of the nodejs_register_toolchains function
# in @rules_js//nodejs:repositories.bzl.
# We do this to have more control over how this stuff is created, to use our
# node mirrors, etc.
def declare_nodejs_repos():
    for name in _VERSIONS:
        node_repositories(
            name = "nodejs_" + name,
            node_repositories = {
                _NODE_VERSION + "-" + name: _VERSIONS[name]
            },
            node_urls = [
                "https://storage.googleapis.com/public-bazel-artifacts/js/node/v{version}/{filename}",
            ],
            node_version = _NODE_VERSION,
            platform = name,
        )
    nodejs_repo_host_os_alias(name = "nodejs", user_node_repository_name = "nodejs")
    nodejs_repo_host_os_alias(name = "nodejs_host", user_node_repository_name = "nodejs")
    toolchains_repo(name = "nodejs_toolchains", user_node_repository_name = "nodejs")
    # NB: npm_import has weird behavior where it transparently makes these
    # copy_directory/copy_to_directory repos for you if you do not set up the
    # repositories beforehand. This is weird behavior that will hopefully be
    # fixed in a later version. For now it's important this function be called
    # before we call into npm_import() in WORKSPACE.
    # Ref: https://github.com/aspect-build/rules_js/blob/a043b6cd1138e608272c2feef8905baf85d86b97/npm/private/npm_import.bzl#L1121
    for plat in _COPY_DIRECTORY_VERSIONS:
        copy_directory_platform_repo(
            name = "copy_directory_" + plat,
            platform = plat,
        )
    for plat in _COPY_TO_DIRECTORY_VERSIONS:
        copy_to_directory_platform_repo(
            name = "copy_to_directory_" + plat,
            platform = plat,
        )
    copy_directory_toolchains_repo(
        name = "copy_directory_toolchains",
        user_repository_name = "copy_directory",
    )
    copy_to_directory_toolchains_repo(
        name = "copy_to_directory_toolchains",
        user_repository_name = "copy_to_directory",
    )
